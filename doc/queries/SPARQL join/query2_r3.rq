PREFIX rmo: <http://eatld.et.tu-dresden.de/rmo#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX rev: <http://test.com/r43ples-dataset-1-revision->
PREFIX : <http://test.com/>

SELECT DISTINCT ?s ?o
WHERE{
    GRAPH ?g_delete_set_full_graph { ?s  :knows  ?o .}
    GRAPH <http://eatld.et.tu-dresden.de/r43ples-revisions> {
        {   
            BIND(rev:5 AS ?r_delete_set)
            [] rmo:references ?r_delete_set ;
                rmo:fullGraph  ?g_delete_set_full_graph.
          
        } UNION {
            VALUES ?r_delete_set {rev:5 rev:4 }
            ?r_delete_set  a  rmo:Revision ;
                rmo:deleteSet  ?g_delete_set_full_graph .
        }
    }
    
    MINUS { 
        GRAPH ?g_add_set { ?s  :knows  ?o .}
        GRAPH <http://eatld.et.tu-dresden.de/r43ples-revisions> {
            VALUES ?r_add_set {rev:5 rev:4 }
            ?r_delete_set prov:wasDerivedFrom* ?r_add_set.
            ?r_add_set  a  rmo:Revision ;
                  rmo:addSet  ?g_add_set .
        }
    }
}
ORDER BY ?s ?o
